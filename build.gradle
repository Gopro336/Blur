buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies { classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT' }
}
plugins {
 	id 'com.matthewprenger.cursegradle' version '1.0.9'
}

apply plugin: 'net.minecraftforge.gradle.forge'

boolean dev = System.getenv('RELEASE') == null || System.getenv('RELEASE').equals('false');
ext.buildnumber = System.getenv().BUILD_NUMBER ?: 'custom';

group = 'com.tterrag.blur'
archivesBaseName = "Blur"
version = "${mod_version}-${buildnumber}"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

minecraft {
    version = "${minecraft_version}-${forge_version}"
	mappings = 'stable_29'
	
    runDir = 'run'

	clientJvmArgs += '-Dfml.coreMods.load=com.tterrag.blur.BlurPlugin'
	
	replace "@VERSION@", project.version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

jar.manifest {
    attributes 'FMLCorePlugin': 'com.tterrag.blur.BlurPlugin'
    attributes 'FMLCorePluginContainsFMLMod': 'true'
}

import org.apache.tools.ant.filters.ReplaceTokens
import groovy.json.JsonSlurper

task createResourcePacks {
	def inputFile = new File("resource_packs.json")
	def json = new JsonSlurper().parseText(inputFile.text)
	json.each {
		def pack_id 	= it.key
		def pack_name   = it.value.name
		def pack_desc 	= it.value.description
		def pack_radius = it.value.radius
		
		def taskName = "createPack" + pack_id.capitalize();
		task "${taskName}" (type: Zip) {
			from ('pack_template') {		
				filter(ReplaceTokens, tokens: [
					mod_version:	project.version.toString(),
					pack_version: 	'3',
					description:	pack_desc.toString(),
					radius: 		pack_radius.toString()
				])
				
				rename(/(.+)\.template/, '$1')
			}
			
			from ('pack_icons') {
				include "${pack_id}.png"
				rename '.+', 'pack.png'
			}
			
			baseName = "Blur " + pack_name
		}
		
		createResourcePacks.finalizedBy taskName
	}
}

// Not necessary atm
// tasks.build.dependsOn createResourcePacks

tasks.curseforge.enabled = !dev && project.hasProperty('curseforge_key')

curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.project_id
        changelogType = 'html'
        changelog = System.getenv('CHANGELOG')
        if (changelog == null || 'none'.equals(changelog)) {
        	changelog = getChangelog() ?: ''
        	changelogType = 'text'
        }
        releaseType = project.release_type
		addGameVersion '1.9.4'
		addGameVersion '1.11.2'
		addGameVersion '1.12'
		addGameVersion '1.12.1'
        mainArtifact(jar) {
            displayName = "Blur ${mod_version}"
        }
    }
}

